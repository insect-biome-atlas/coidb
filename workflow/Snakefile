# Validate config

from snakemake.utils import validate
validate(config, "schemas/config.schema.yaml")

import polars as pl
import string

rule all:
    input:
        "_processed/03-data.consensus_taxonomy.tsv"

checkpoint extract_input:
    input:
        tarball=config["input_file"],
    output:
        directory("_extract")
    log:
        "_logs/extract_input.log"
    shell:
        """
        tar -C {output} -xvf {input.tarball} > {log} 2>&1
        """

def get_extracted(wildcards):
    checkpoint_output = checkpoints.extract_input.get(**wildcards).output[0]
    tsv = expand(
        "_extract/{f}.tsv",
        f=glob_wildcards(os.path.join(checkpoint_output, "{f}.tsv")).f
    )
    return tsv

rule filter:
    message: "Filter data to only include BOLD BIN records, trim and remove spurious sequences"
    input:
        tsv=get_extracted
    output:
        tsv="_processed/01-data.filtered.tsv"
    log:
        "_logs/filter.log"
    params:
        src=workflow.source_path("scripts/filter.py"),
        min_len=config["min_len"]
    shell:
        """
        python {params.src} -i {input.tsv} -o {output.tsv} -l {params.min_len} 2>{log}
        """

rule fill_missing:
    message: "Filling missing ranks"
    input:
        tsv=rules.filter.output.tsv
    output:
        tsv="_processed/02-data.fill_missing.tsv"
    log:
        "_logs/fill_missing.log"
    params:
        src=workflow.source_path("scripts/fill_missing.py")
    shell:
        """
        python {params.src} -i {input.tsv} -o {output.tsv} 2>{log}
        """

rule calculate_consensus:
    message: "Calculating consensus taxonomy for BINs"
    input:
        tsv=rules.fill_missing.output.tsv,
    output:
        tsv="_processed/03-data.consensus_taxonomy.tsv"
    log:
        "_logs/calculate_consensus.log"
    params:
        src=workflow.source_path("scripts/consensus_taxonomy.py"),
        threshold=config["consensus_threshold"]
    threads: workflow.cores
    shell:
        """
        python {params.src} -i {input.tsv} -o {output.tsv} -p {threads} -t {params.threshold} 2>{log}
        """